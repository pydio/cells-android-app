buildscript {

    // Define the version of the libraries that we use.
    // Rather implement a BOM mechanism that also takes care of having the java-sdk locally or not.
    ext {
        // Global variables: build plugins
        kotlin_version = '1.9.22'
        gradle_build_tools_version = '8.3.0'

        // Local variables (use def)
        def lifecycle_version = '2.6.2'
        def coroutines_android_version = '1.7.3'
        def room_version = '2.6.1'
        def work_version = '2.9.0'

        def appcompat_version = '1.6.1'
        def androidx_core_version = '1.12.0'
        def preference_version = '1.2.1'
        def splash_version = '1.0.1'

        def material_version = '1.11.0'

        // Compose versions is defined via platform BOM
        // We define here the versions of a few extra libraries that extend the Jetpack Compose features
        def navigation_version = '2.7.6'
        def compose_nav_version = '2.7.6'

        // Define All non-Android Third Party versions in a single place
        def aws_s3_sdk_version = '2.74.0'
        def glide_version = '5.0.0-rc01'
        def compose_glide_version = '1.0.0-beta01'
        def zxing_version = '3.5.2'
        def gson_version = '2.10.1'
        def commons_codec_version = '1.16.1'

        // Test libraries
        def junit_version = '4.13.2'
        def truth_version = '1.2.0'
        def robolectric_version = '4.11.1'
        def mockito_version = '5.1.1'
        def androidx_core_testing_version = '2.2.0'
        def androidx_test_version = '1.5.0'
        def androidx_test_runner_version = '1.5.2'
        def androidx_test_ext_version = '1.1.5'
        def espresso_version = '3.5.1'

        // Define dependency packages that are then used as shortcuts by the various modules.
        librariesKapt = [
                "androidx.room:room-compiler:$room_version",
                // Might be a lead to follow to fix the warning seen at build time.
                "androidx.room:room-runtime:$room_version",
                "com.github.bumptech.glide:compiler:$glide_version",
        ]

        processors = [
                "com.github.bumptech.glide:compiler:$glide_version",
        ]

        libraries = [
                // Kotlin standard library and coroutines
                "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
                "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_android_version",
                "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version",

                // Android UI and appcompat
                "androidx.core:core:$androidx_core_version",
                "androidx.appcompat:appcompat:$appcompat_version",
                // Navigation UI
                "androidx.navigation:navigation-ui-ktx:$navigation_version",
                // ViewModel
                "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version",
                // Extended UI features: prefs and splash screen
                "androidx.preference:preference:$preference_version",
                "androidx.core:core-splashscreen:$splash_version",

                // Material UI => we still need this to build custom palette based on remote server custom primary color
                "com.google.android.material:material:$material_version",

                // Room for database, work for background
                "androidx.room:room-ktx:$room_version",
                "androidx.work:work-runtime-ktx:$work_version",

                // Dependency injection with Koin https://insert-koin.io/
                // We now use the Koin BOM
                "io.insert-koin:koin-core",
                "io.insert-koin:koin-android",
                "io.insert-koin:koin-androidx-workmanager",
                "io.insert-koin:koin-androidx-navigation",

                // Files
                "com.amazonaws:aws-android-sdk-s3:$aws_s3_sdk_version",
                "com.github.bumptech.glide:glide:$glide_version",

                // Utils
                "commons-codec:commons-codec:$commons_codec_version",
                "com.google.code.gson:gson:$gson_version",
                // QRCode generation
                "com.google.zxing:core:$zxing_version",
        ]

        // We rely on a BOM to provide correct versions of the Jetpack Compose libraries.
        // See cells-client gradle config
        composeLibraries = [
                "androidx.compose.runtime:runtime",
                "androidx.compose.ui:ui",
                "androidx.compose.ui:ui-util",
                "androidx.compose.ui:ui-tooling",
                "androidx.compose.foundation:foundation",
                "androidx.compose.foundation:foundation-layout",
                "androidx.compose.material3:material3",
                "androidx.compose.material3:material3-window-size-class",
                // We use material 3, so remove remaining code that depend on this and remove the dep.
                "androidx.compose.material:material",
                // This library is also provided as an API dependency by androidx.compose.material:material.
                // We specify it explicitly so that we won't break when we finally remove material lib from
                // the explicit dependency tree
                "androidx.compose.material:material-icons-core",
                // this separate library, the full set of Material icons. Due to the very large size of this lib,
                // Make sure to use R8/Proguard to strip unused icons when including this as a direct dependency.
                // See https://developer.android.com/reference/kotlin/androidx/compose/material/icons/package-summary
                "androidx.compose.material:material-icons-extended",

                // Include extensions the permits integration of Compose with extra TP libraries
                "androidx.navigation:navigation-compose:$compose_nav_version",
                "com.github.bumptech.glide:compose:$compose_glide_version",
//                "io.insert-koin:koin-androidx-compose:$compose_koin_version",
                "io.insert-koin:koin-androidx-compose",
        ]

        testLibraries = [
                "androidx.arch.core:core-testing:$androidx_core_testing_version",
                // Junit 4
                "junit:junit:$junit_version",
                // Assertions
                "com.google.truth:truth:$truth_version",
                // Coroutines testing
                "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_android_version",
                // Dependency injection with Koin
                "io.insert-koin:koin-test",
                "io.insert-koin:koin-test-junit4",
                // Mocks
                "org.mockito:mockito-core:$mockito_version",

        ]

        androidTestLibraries = [
                // Koin for Test + JUnit 4
                "junit:junit:$junit_version",
                "io.insert-koin:koin-test",
                "io.insert-koin:koin-test-junit4",

                //  Androidx Testing
                "androidx.arch.core:core-testing:$androidx_core_testing_version",
                "androidx.test:runner:$androidx_test_runner_version",
                "androidx.test:rules:$androidx_test_version",
                "androidx.test.ext:junit:$androidx_test_ext_version",
                "androidx.work:work-testing:$work_version",

                // Coroutines testing
                "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_android_version",

                // Espresso
                "androidx.test.espresso:espresso-core:$espresso_version",
                "androidx.test.espresso:espresso-contrib:$espresso_version",
                "androidx.test.espresso:espresso-intents:$espresso_version",

                "org.robolectric:robolectric:$robolectric_version",
        ]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_build_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    // Publish the generated resources as Maven Artifacts.
    id 'maven-publish'
}

// Retrieve local context
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))
retrieveFromEnv(localProperties) // There must be a more elegant way to do this...

def versionProperties = new Properties()
versionProperties.load(new FileInputStream(rootProject.file("versions.properties")))

// Manage optional local sdk-java submodule
def sdkJavaCheck = new File('sdk-java/sdk-java.gradle')

// Expose custom properties to all modules in the project.
ext {
    githubUrl = 'https://github.com/pydio/cells-android-app.git'

    // Expose properties retrieved from files to child projects
    configs = localProperties
    ownVersions = versionProperties

    // Shortcut to enable building the Android layers without checking out the sdk-java repository
    useLocalSdkJava = sdkJavaCheck.exists()
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

/* Factorise some of the methods used in various sub projects */
// Pre-create generic common pom configuration:
ext.getBasePom = { repoURL ->
    return {
        licenses {
            license {
                name "The GNU Lesser General Public License, Version 3.0"
                url "http://www.gnu.org/licenses/lgpl-3.0.txt"
                distribution "repo"
            }
        }

        developers {
            developer {
                id "bsinou"
                name "Bruno Sinou"
                email "bruno.sinou@posteo.de"
            }

            developer {
                id "cdujeu"
                name "Charles du Jeu"
                email "charles@ajaxplorer.info"
            }
        }

        scm {
            url repoURL
        }
    }
}

// Override local.properties sensitive values with environment parameter
def static retrieveFromEnv(Properties props) {

    def tmpValue = System.getenv('ANDROID_KEYSTORE_PATH')
    if (tmpValue != null) {
        props['keystore.path'] = tmpValue
    }
    tmpValue = System.getenv('ANDROID_KEYSTORE_PWD')
    if (tmpValue != null) {
        props['keystore.pwd'] = tmpValue
    }
    tmpValue = System.getenv('ANDROID_SIGNKEY_ALIAS')
    if (tmpValue != null) {
        props['signkey.alias'] = tmpValue
    }
    tmpValue = System.getenv('ANDROID_SIGNKEY_PWD')
    if (tmpValue != null) {
        props['signkey.pwd'] = tmpValue
    }
}
